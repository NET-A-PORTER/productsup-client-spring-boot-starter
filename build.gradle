plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0-rc-1'
    id "org.sonarqube" version "4.2.1.3168"
}

group = 'io.github.shinusuresh'
version = '0.1.1'
description = 'Spring boot library for ProductsUp platform'
java.sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    api "org.springframework.boot:spring-boot-autoconfigure:3.1.2"
    api "org.springframework.boot:spring-boot-starter-webflux:3.1.2"
    api "org.springframework:spring-context-support:6.0.11"
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.2'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mock-server:mockserver-junit-jupiter:5.15.0'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {

        mavenJava(MavenPublication) {
            from(components["java"])
            pom {

                afterEvaluate {
                    name = project.description
                    description = project.description
                }
                url = 'https://github.com/shinusuresh/productsup-client-spring-boot-starter.git'

                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                developers {
                    developer {
                        id = 'shinusuresh'
                        name = 'Shinu Suresh'
                        email = 'mailtoshinu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/shinusuresh/productsup-client-spring-boot-starter.git'
                    developerConnection = 'scm:git:git://github.com/shinusuresh/productsup-client-spring-boot-starter.git'
                    url = 'https://github.com/shinusuresh/productsup-client-spring-boot-starter.git'
                }
            }
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "shinusuresh_productsup-client-spring-boot-starter"
        property "sonar.organization", "shinusuresh"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
