plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0-rc-1'
}

group = 'io.github.net-a-porter'
version = hasProperty('version') ? property('version') : '0.1.1'
description = 'Spring boot library for ProductsUp platform'
java.sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    api "org.springframework.boot:spring-boot-autoconfigure:3.1.2"
    api "org.springframework.boot:spring-boot-starter-webflux:3.1.2"
    api "org.springframework:spring-context-support:6.0.11"
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.2'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mock-server:mockserver-junit-jupiter:5.15.0'
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {

        mavenJava(MavenPublication) {
            from(components["java"])
            pom {

                afterEvaluate {
                    name = project.description
                    description = project.description
                }
                url = 'https://github.com/NET-A-PORTER/productsup-client-spring-boot-starter.git'

                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                developers {
                    developer {
                        id = 'shinusuresh'
                        name = 'Shinu Suresh'
                        email = 'shinu.suresh@ynap.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/NET-A-PORTER/productsup-client-spring-boot-starter.git'
                    developerConnection = 'scm:git:git://github.com/NET-A-PORTER/productsup-client-spring-boot-starter.git'
                    url = 'https://github.com/NET-A-PORTER/productsup-client-spring-boot-starter.git'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
